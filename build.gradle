plugins {
    id 'java'
    id 'application'
}


group = 'net.modularmods'
version = '1.0-SNAPSHOT'

project.ext.lwjglVersion = "3.3.4-SNAPSHOT"
project.ext.jomlVersion = "1.10.5"

// Determine the appropriate natives based on the OS
switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos-arm64"
        break
        // You can add more cases for other operating systems if needed
    default:
        throw new GradleException("Unsupported operating system")
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-ktx"
    implementation "org.lwjgl:lwjgl-zstd"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-ktx::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-zstd::$lwjglNatives"

    implementation "org.joml:joml:${jomlVersion}"
}

// Define the run task to execute the application
application {
    mainClassName = "net.modularmods.protoengine.Main" // Use the main class defined above
    //For Mac OS
    //applicationDefaultJvmArgs = ["-XstartOnFirstThread", "-Xmx512m"]
}